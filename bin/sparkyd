sub MAIN (
  Str  :$root = '/home/' ~ %*ENV<USER> ~ '/.sparky/projects', 
  Str  :$work-root = '/home/' ~ %*ENV<USER> ~ '/.sparky/work', 
  Int  :$timeout = %*ENV<SPARKY_TIMEOUT> || 10,
)

{


  react {

    mkdir $root;
    mkdir $work-root;

    sub run-project($dir, $project) {
      #say "proceed $dir ...";
      mkdir "$work-root/$project";
      whenever Proc::Async.new(
        'sparky-runner.pl6', 
        "--dir=$dir", 
        "--make-report",
        "--timeout=$timeout"
      ).start {
        run-project($dir, $project);
      }
    }
    
    sub add-dirs() {
      state %seen;
      for dir($root) -> $dir {
        next if "$dir".IO ~~ :f;
        next if %seen{$dir}++;
        next if $dir.basename eq '.git';
        next if $dir.basename eq '.reports';
        run-project($dir, $dir.basename);
      }
    }
    
    whenever IO::Notification.watch-path($root) {
      add-dirs();
    }

    add-dirs();
  }
} 

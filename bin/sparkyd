sub MAIN (
  Str  :$root = '/home/' ~ %*ENV<USER> ~ '/.sparky/projects', 
  Str  :$work-root = '/home/' ~ %*ENV<USER> ~ '/.sparky/work', 
  Str  :$reports-root = '/home/' ~ %*ENV<USER> ~ '/.sparky/reports',
  Int  :$timeout = 300,
)

{


  react {

    mkdir $root;
    mkdir $work-root;

    sub run-project($dir, $project) {
      mkdir "$work-root/$project";
      whenever Proc::Async.new(
        'sparky-runner.pl6', 
        "--dir=$dir", 
        "--project=$project",
        "--reports-root=$reports-root"
      ).start {
        Promise.in($timeout){ run-project($dir, $project) } if "$dir/sparrowfile".IO ~~ :f ;
      }
    }
    
    sub add-dirs() {
      state %seen;
      for dir($root) -> $dir {
        next if %seen{$dir}++;
        next unless "$dir/sparrowfile".IO ~~ :f;
        run-project($dir, $dir.basename);
      }
    }
    
    whenever IO::Notification.watch-path($root) {
      add-dirs();
    }

    add-dirs();
  }
} 
